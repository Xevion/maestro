plugins {
    id "com.github.johnrengelman.shadow" version "8.0.0"
}

archivesBaseName = archivesBaseName + "-forge"

unimined.minecraft {
    minecraftForge {
        loader project.forge_version
        mixinConfig["mixins.maestro.json"]
    }

    runs {
        off = false
    }
}

//loom {
//    forge {
//        mixinConfig 'mixins.maestro.json'
//    }
//}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

dependencies {
    // because of multiple source-sets `common project(":")` doesn't work
    for (final sourceSet in rootProject.sourceSets) {
        if (sourceSet == rootProject.sourceSets.test) continue
        if (sourceSet == rootProject.sourceSets.schematica_api) continue
        common sourceSet.output
        shadowCommon sourceSet.output
    }
    shadowCommon "dev.babbaj:nether-pathfinder:${project.nether_pathfinder_version}"
    shadowCommon 'org.jetbrains.kotlin:kotlin-stdlib'
    shadowCommon('io.github.llamalad7:mixinextras-forge:0.4.1') {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"

    manifest {
        attributes(
                'MixinConfigs': 'mixins.maestro.json',
                "MixinConnector": "maestro.launch.MaestroMixinConnector",

                'Implementation-Title': 'Baritone',
                'Implementation-Version': version,
        )
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}