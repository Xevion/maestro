plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

unimined.minecraft {
    runs.client = {
        mainClass = 'net.minecraft.launchwrapper.Launch'
        args.addAll(['--tweakClass', 'maestro.launch.tweaker.MaestroTweaker'])
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

dependencies {
    implementation "org.spongepowered:mixin:${project.mixin_version}"

    // for some reason mixin isn't including these...
    implementation "org.ow2.asm:asm:${project.asm_version}"
    implementation "org.ow2.asm:asm-tree:${project.asm_version}"
    implementation "org.ow2.asm:asm-commons:${project.asm_version}"
    implementation "org.ow2.asm:asm-util:${project.asm_version}"
    implementation "org.ow2.asm:asm-analysis:${project.asm_version}"

    implementation 'io.github.llamalad7:mixinextras-common:0.4.1'

    implementation 'com.github.ImpactDevelopment:SimpleTweaker:1.2'
    implementation('net.minecraft:launchwrapper:of-2.3') {
        exclude module: 'lwjgl'
        exclude module: 'asm-debug-all'
    }

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // because of multiple sourcesets `common project(":")` doesn't work
    for (sourceSet in rootProject.sourceSets) {
        if (sourceSet == rootProject.sourceSets.test) continue
        if (sourceSet == rootProject.sourceSets.schematica_api) continue
        common sourceSet.output
        shadowCommon sourceSet.output
    }
    shadowCommon 'org.jetbrains.kotlin:kotlin-stdlib'
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set 'dev-shadow'
}


remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set 'dev'

    preserveFileTimestamps = false
    reproducibleFileOrder = true

    manifest {
        attributes(
                'MixinConfigs': 'mixins.maestro.json',
                "MixinConnector": 'maestro.launch.MaestroMixinConnector',

                'Implementation-Title': 'Baritone',
                'Implementation-Version': version,
        )
    }
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = rootProject.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}