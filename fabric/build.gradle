plugins {
    id 'com.gradleup.shadow' version '9.2.2'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowCommon(project(path: ':common', configuration: 'transformProductionFabric')) { transitive = false }

    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:0.116.1+1.21.4"
    modImplementation("dev.architectury:architectury-fabric:15.0.1")

    // Kotlin - required at runtime
    modImplementation 'org.jetbrains.kotlin:kotlin-stdlib:2.2.21'

    // Logback - required for LoggerConfigurator in API package
    modImplementation 'org.slf4j:slf4j-api:2.0.17'
    modImplementation 'ch.qos.logback:logback-classic:1.5.21'

    // gRPC and Protocol Buffers - required for coordination system
    modImplementation 'io.grpc:grpc-netty-shaded:1.77.0'
    modImplementation 'io.grpc:grpc-protobuf:1.60.0'
    modImplementation 'io.grpc:grpc-stub:1.60.0'
    modImplementation 'io.grpc:grpc-kotlin-stub:1.4.1'
    modImplementation 'com.google.protobuf:protobuf-kotlin:4.28.2'
    modImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0'

    // Other runtime dependencies
    modImplementation "dev.babbaj:nether-pathfinder:${project.nether_pathfinder_version}"
    modImplementation 'me.xdrop:fuzzywuzzy:1.4.0'

    // Shaded dependencies
    include "dev.babbaj:nether-pathfinder:${project.nether_pathfinder_version}"
    include 'me.xdrop:fuzzywuzzy:1.4.0'
    include 'io.github.llamalad7:mixinextras-fabric:0.4.1'
    include 'org.jetbrains.kotlin:kotlin-stdlib:2.2.21'
    include 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0'
    include 'ch.qos.logback:logback-classic:1.5.21'
    include 'ch.qos.logback:logback-core:1.5.19'
    include 'io.grpc:grpc-netty-shaded:1.77.0'
    include 'io.grpc:grpc-protobuf:1.60.0'
    include 'io.grpc:grpc-stub:1.60.0'
    include 'io.grpc:grpc-kotlin-stub:1.4.1'
    include 'com.google.protobuf:protobuf-kotlin:4.28.2'
}

processResources {
    inputs.property 'version', project.version
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = 'dev'
}

afterEvaluate {
    components.java {
        withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
            skip()
        }
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + '-fabric'
            from components.java
        }
    }
    repositories {}
}
