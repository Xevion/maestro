plugins {
    id 'com.diffplug.spotless' version '7.0.3' apply false
    id 'org.jetbrains.kotlin.jvm' version '2.1.0' apply false
}

// Spotless requires Java 21+ to parse modern Java syntax
// See: https://github.com/diffplug/spotless/issues/724
if (JavaVersion.current() < JavaVersion.VERSION_21) {
    throw new GradleException(
        "This project requires Java 21 or later to run Spotless (currently running ${JavaVersion.current()}).\n" +
        "Please set JAVA_HOME to a Java 21+ installation or run with: ./gradlew -Dorg.gradle.java.home=/path/to/jdk21"
    )
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: "xyz.wagyourtail.unimined"
    apply plugin: "maven-publish"
    apply plugin: 'com.diffplug.spotless'

    archivesBaseName = rootProject.archives_base_name

    def vers = ""
    try {
        vers = 'git describe --always --tags --first-parent --dirty'.execute().text.trim()
    } catch (Exception e) {
        println "Version detection failed: " + e
    }
    if (!vers.startsWith("v")) {
        println "using version number: " + rootProject.mod_version
        version = rootProject.mod_version
    } else {
        version = vers.substring(1)
        println "Detected version " + version
    }
    group = rootProject.maven_group

    sourceCompatibility = targetCompatibility = JavaVersion.toVersion(project.java_version)

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(sourceCompatibility.majorVersion.toInteger()))
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = project.java_version
            freeCompilerArgs += ['-Xjvm-default=all']
        }
    }

    repositories {
        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'fabric-maven'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'impactdevelopment-repo'
            url = 'https://impactdevelopment.github.io/maven/'
        }
        maven {
            name = "ldtteam"
            url = "https://maven.parchmentmc.net/"
        }
        // for the newer version of launchwrapper
        maven {
            name = "multimc-maven"
            url = "https://files.multimc.org/maven/"
            metadataSources {
                artifact()
            }
        }
        mavenCentral()
        maven {
            name = 'babbaj-repo'
            url = 'https://babbaj.github.io/maven/'
        }
    }

    dependencies {
        compileOnly "org.spongepowered:mixin:${project.mixin_version}"
        compileOnly "org.ow2.asm:asm:${project.asm_version}"

        implementation "dev.babbaj:nether-pathfinder:${project.nether_pathfinder_version}"

        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    }

    unimined.minecraft(sourceSets.main, true) {
        version rootProject.minecraft_version

        mappings {
            intermediary()
            mojmap()
            parchment("1.20.6", "2024.05.01")

            devFallbackNamespace "official"
        }
    }

    spotless {
        enforceCheck = false
        java {
            target 'src/*/java/**/*.java'
            targetExclude 'src/launch/java/maestro/launch/mixins/MixinLootContextBuilder.java'
            googleJavaFormat('1.32.0').aosp().reflowLongStrings()
            removeUnusedImports()
            toggleOffOn()
        }
        kotlin {
            target 'src/*/kotlin/**/*.kt'
            ktlint('1.5.0')
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"

        def targetVersion = project.java_version.toInteger()
        if (JavaVersion.current().isJava9Compatible()) {
            it.options.release = targetVersion
        }
    }
}

unimined.minecraft {
    runs.off = true
    defaultRemapJar = false
}

archivesBaseName = archivesBaseName + "-common"

sourceSets {
    api {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
    main {
        compileClasspath += api.output
        runtimeClasspath += api.output
    }
    test {
        compileClasspath += main.compileClasspath + main.runtimeClasspath + main.output
        runtimeClasspath += main.compileClasspath + main.runtimeClasspath + main.output
    }
    launch {
        compileClasspath += main.compileClasspath + main.runtimeClasspath + main.output
        runtimeClasspath += main.compileClasspath + main.runtimeClasspath + main.output
    }
    schematica_api {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
    main {
        compileClasspath += schematica_api.output
        runtimeClasspath += schematica_api.output
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

jar {
    from sourceSets.main.output, sourceSets.launch.output, sourceSets.api.output
}

javadoc {
    options.addStringOption('Xwerror', '-quiet') // makes the build fail on travis when there is a javadoc error
    options.linkSource true
    options.encoding "UTF-8" // allow emoji in comments :^)
    source = sourceSets.api.allJava
    classpath += sourceSets.api.compileClasspath
}
