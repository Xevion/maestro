plugins {
    id 'java'
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'com.diffplug.spotless' version '7.0.3'
    id 'org.jetbrains.kotlin.jvm' version '2.2.21' apply false
    id 'com.google.protobuf' version '0.9.4' apply false
}

architectury {
    minecraft = minecraft_version
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

if (JavaVersion.current() < JavaVersion.VERSION_21) {
    throw new GradleException("This project requires Java 21+")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        mavenCentral()
        maven { url = 'https://maven.fabricmc.net/' }
        maven { url = 'https://maven.architectury.dev/' }
        maven { url = 'https://maven.neoforged.net/releases/' }
        maven { url = 'https://maven.parchmentmc.net/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://babbaj.github.io/maven/' }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = project.java_version.toInteger()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.java_version.toInteger())
        }
        withSourcesJar()
    }

    tasks.withType(Jar).configureEach {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'com.diffplug.spotless'

    configurations.configureEach {
        resolutionStrategy {
            force 'com.google.guava:guava:33.0.0-jre'
        }
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.20.6:2024.05.01@zip")
        }
    }

    spotless {
        enforceCheck = false
        java {
            target 'src/**/*.java'
            googleJavaFormat('1.32.0').aosp().reflowLongStrings()
            removeUnusedImports()
            toggleOffOn()
        }
        kotlin {
            target 'src/**/*.kt'
            ktlint('1.5.0')
        }
    }
}
